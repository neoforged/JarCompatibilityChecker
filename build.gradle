import net.neoforged.gradleutils.PomUtilsExtension.License

plugins {
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.neoforged.gradleutils' version '3.0.0-alpha.10'
}

group 'net.neoforged'

base.archivesName = 'jarcompatibilitychecker'

version = gradleutils.version
println('Version: ' + version)
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

gradleutils {
    setupSigning()
}

ext {
    CHANGELOG_TAG = '0.1'
}

repositories {
    mavenCentral()
}

configurations {
    testCp
}

dependencies {
    api 'org.ow2.asm:asm-commons:9.3'
    api 'net.sf.jopt-simple:jopt-simple:5.0.4'
    api 'com.google.code.gson:gson:2.9.0'
    api 'com.google.guava:guava:31.1-jre'
    implementation 'org.jetbrains:annotations:23.0.0'

    testImplementation 'junit:junit:4.13.2'

    testCp(files(tasks.shadowJar.archiveFile))
}

jar {
    from(project.rootDir) {
        include 'LICENSE'
    }

    manifest {
        attributes([
                'Main-Class': 'net.neoforged.jarcompatibilitychecker.ConsoleTool',
                'Implementation-Version': project.version
        ])
    }
}

shadowJar {
    from(project.rootDir) {
        include 'LICENSE'
    }

    minimize()

    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/versions/9/module-info.class'
}

assemble.dependsOn shadowJar

java.withSourcesJar()

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.register('runExampleTest', JavaExec) {
    classpath = configurations.testCp
    mainClass = 'net.neoforged.jarcompatibilitychecker.ConsoleTool'
    args('--binary', '--output', 'output.json', '--base-jar', 'tests/base/build/libs/base.jar', '--input-jar', 'tests/top/build/libs/top.jar')
    dependsOn(':tests:base:jar', ':tests:top:jar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jarcompatibilitychecker'
            from components.java
            gradleutils.sign(it)

            pom {
                name = project.name
                description = 'A Java library for finding incompatibilities between binaries'

                pomUtils.neoForgedDeveloper(it)
                pomUtils.license(it, License.LGPL_v2)
                pomUtils.githubRepo(it, 'JarCompatibilityChecker')
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}

changelog {
    from CHANGELOG_TAG
}
