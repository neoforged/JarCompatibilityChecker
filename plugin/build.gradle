plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.1.0'
}

version rootProject.version
group 'net.neoforged.jarcompatibilitychecker'

if (System.getenv('GPP_KEY')) {
    project.ext {
        set('gradle.publish.key', System.getenv('GPP_KEY'))
        set('gradle.publish.secret', System.getenv('GPP_SECRET'))
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(':'))
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.apache.maven:maven-artifact:3.9.11'
}

gradlePlugin {
    website = 'https://github.com/neoforged/JarCompatibilityChecker'
    vcsUrl = 'https://github.com/neoforged/JarCompatibilityChecker.git'

    plugins {
        jcc {
            id = 'net.neoforged.jarcompatibilitychecker'
            displayName = 'JarCompatibilityChecker'
            description = 'A plugin that checks if two jars are compatible'
            tags.set(['compatibility'])
            implementationClass = 'net.neoforged.jarcompatibilitychecker.gradle.JCCPlugin'
        }
    }
}

publishing {
    repositories {
        maven gradleutils.publishingMaven
    }
}

gradle.taskGraph.whenReady {
    tasks.withType(PublishToMavenRepository) { PublishToMavenRepository t ->
        if (t.repository.name == "sonatype") {
            logger.lifecycle("Disabling task `{}` because it publishes to Sonatype", t.path)
            t.enabled = false
        }
    }
}
